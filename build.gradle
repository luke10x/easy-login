plugins {
    id 'java'
    id 'war'
    id 'maven-publish' // pom will be generated for this build for Arquillian
}

compileJava {
    sourceCompatibility = 17
    targetCompatibility = 17
}

repositories {
    mavenCentral()
}

allprojects {
    tasks.withType(JavaCompile) {
        options.deprecation = true
    }
}

configurations {
    wildFlyDistribution
}
dependencies {
    providedCompile 'jakarta.platform:jakarta.jakartaee-api:10.0.0'
    implementation 'jakarta.mvc:jakarta.mvc-api:2.1.0'
    implementation 'org.eclipse.krazo:krazo-core:3.0.1'
    implementation 'org.eclipse.krazo:krazo-resteasy:3.0.1'

    // For QR generation
    implementation 'com.google.zxing:core:3.5.1'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.28'
    annotationProcessor 'org.projectlombok:lombok:1.18.28'

    // JUnit 5 dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'

    // Mockito dependency
    testImplementation 'org.mockito:mockito-core:5.2.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.3.1'

    // For Arquillian
    testImplementation 'org.jboss.shrinkwrap.resolver:shrinkwrap-resolver-maven-plugin:3.1.4'

    // Generic Arquillian dependencies
    implementation platform('org.jboss.arquillian:arquillian-bom:1.7.0.Final')
    testImplementation 'org.jboss.arquillian.junit5:arquillian-junit5-container'
    testImplementation 'org.jboss.arquillian.protocol:arquillian-protocol-servlet-jakarta'
    testImplementation 'org.jboss.shrinkwrap.resolver:shrinkwrap-resolver-impl-gradle-embedded-archive:3.1.4'
    testImplementation 'org.gradle:gradle-tooling-api:7.3-20210825160000+0000'

    // Just Wildfly distribution special scope
    wildFlyDistribution 'org.wildfly:wildfly-dist:27.0.1.Final@zip'
}


task setupWildFly(type: Copy) {
    from configurations.wildFlyDistribution.resolvedConfiguration.files.collect { file ->
        zipTree(file)
    }
    into "${buildDir}/wildfly-27.0.1"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

// Penalty for using gradle:
// Aquillian needs this pom to create shrinkwrap archive
publishing {
    publications {
        sample(MavenPublication) {
            from components.java

            groupId 'org.example'
            artifactId 'test'
            version '1.0.0'
        }
    }
}

task writeNewPom {
    dependsOn 'generatePomFileForSamplePublication'
    doLast {
    }
}

test {
    dependsOn setupWildFly
    dependsOn writeNewPom
    systemProperty 'arquillian.launch', 'wildfly-managed'
    systemProperty 'serverConfig', 'standalone-full.xml'
//    systemProperty 'jboss.home', file("${buildDir}/wildfly-27.0.1").path
//    environment 'JBOSS_HOME', file("${buildDir}/wildfly-27.0.1").path
//    environment 'WILDFLY_HOME', file("${buildDir}/wildfly-27.0.1").path
}